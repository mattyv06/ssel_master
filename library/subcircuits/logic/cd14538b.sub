*  D:\Daten\Ltc\CD4000\CD4000.lib
*
*  CD40xxx Model libraray for LTSPICE from www.linear.com/software
*
*  Revision 0.61 09/24/2005     CD4538B added, CD14538B reset removed from trigger
*  Revision 0.60 04/14/2005     CD4020B, CD4024B, CD4040B clock corrected
*  Revision 0.59 04/14/2005     CD4020B, CD4024B, CD4040B clock inverted
*  Revision 0.58 03/29/2005     CD4017B inverted output Q59
*  Revision 0.57 05/03/2004     CD4008B added
*  Revision 0.56 08/29/2003
*
*
*  All parts have been divided into three sections.
*
*  >--| FILTER/LEVEL |----| 1V-LOGIC Axx |----| OUTPUT LEVEL |-->
*
*  Timings are based on TI data sheets. http://www.ti.com
*
*  Delays are given for Vdd = 5V.
*  Used delay:  Td = (Tpd-Tr/2-Tfilt)*5/(Vdd)
*  The gate delay has to be set to tpd minus 5ns for the input filter
*  and another minus 25ns for Trise/2
*    td1 = tpd  - 40ns - 10ns
*
.SUBCKT CD14538B CGND1 RC1 CLR1 A1 B1 Q1 QN1 VGND QN2 Q2 B2 A2 CLR2 RC2 CGND2 VDD vdd1={vdd} speed1={speed} tripdt1={tripdt}
.param egain=1/{vdd1}
.param td5=1e-9*(50)*5.0/{vdd1}*{speed1}
.param td2=1e-9*(20)*5.0/{vdd1}*{speed1}
*
XINA1    A1   A1i    VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1}
XINB1    B1   B1i    VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1}
XINCLR1  CLR1 CLR1i  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1}
XINRC11  RC1  RC1i   VDD VGND  CD40_IN_0  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1}
*
A41 DHIGH1 0 N0141 0 N0041 N0061 N0051 0 DFLOP   tripdt={tripdt1}  td={td5}
*A3 0 N015 N016 N023 0  N014 0 0 OR  tripdt={tripdt1}  td={td5}
A31 0 N0151 N0161 0 0     0 N0141 0  OR  tripdt={tripdt1}  td={td5}
A11 A1i 0 0 0 0          0 N0151 0  SCHMITT  Vt=0.46 Vh=0.02  tripdt={tripdt1}  td={td5}
A21 B1i 0 0 0 0          N0161 0 0  SCHMITT  Vt=0.46 Vh=0.02  tripdt={tripdt1}  td={td5}
A61 0 N0081 0 N0061 0     N0071 0 0  AND  tripdt={tripdt1}  td={td5}
A51 0 N0091 0 N0071 0     N0081 0 0  AND  tripdt={tripdt1}  td={td5}
A141 0 N0031 0 N0211 0    N0041 0 0  AND  tripdt={tripdt1}  td={td2}
A131 0 N0031 0 N0051 0    0 N0301 0  AND  tripdt={tripdt1}  td={td5}
A121 N0051 0 0 0 0       N0121 0 0  BUF  tripdt={tripdt1}  td={td5}
A211 0 N0101 0 N0111 0    0 N0091 0  OR  tripdt={tripdt1}  td={td5}
A221 0 N0051 0 N0071 0    N0101 0 0  OR  tripdt={tripdt1}  td={td5}
A111 0 N0131 0 N0121 0    0 N0031 0  OR  tripdt={tripdt1}  td={td5}
A71 0 N0071 0 N0181 0     QN1i Q1i 0  AND  tripdt={tripdt1}  td={td5}
A201 RC1i 0 0 0 0        N0111 0 0  SCHMITT  tripdt={tripdt1}  td={td5}  Vt=0.65 Vh=0.2m
A101 RC1i 0 0 0 0        0 N0131 0  SCHMITT  tripdt={tripdt1}  td={td5}  Vt=0.1 Vh=0.2m
A321 0 N0061 0 N0211 0    N0221 0 0  OR  tripdt={tripdt1}  td={td5}
A311 0 N0221 0 N0231 0    N0211 0 0  OR  tripdt={tripdt1}  td={td2}
A331 N0211 0 0 0 0       N0191 0 0  BUF  tripdt={tripdt1}  td={td5}
A301 CLR1i 0 0 0 0       N0231 0 0  BUF  tripdt={tripdt1}  td={td5}
A341 0 N0071 0 N0061 0    N0201 0 0  AND  tripdt={tripdt1}  td={td5}
A351 0 N0201 0 N0181 0    N0171 0 0  OR  tripdt={tripdt1}  td={td5}
A361 0 N0171 0 N0191 0    N0181 0 0  OR  tripdt={tripdt1}  td={td5}
M101 RC1 N0011 VGND VGND NMOS1
M301 RC N0241 VDD VDD PMOS1
D11 RC1 VDD DIO1
D21 VGND RC1 DIO1
R101 N0011 N0021 100
R301 N0241 N0291 100
R31 CGND1 VGND 1
E41 RC1i 0 RC1 VGND {egain}
E71 N0291 VDD N0181 0 {VDD1}
E81 N0021 VGND N0301 0 {VDD1}
V11 DHIGH1 0 DC 1
*
XOUT01  Q1i  Q1   VDD VGND  CD40_OUT_1X  VDD2={VDD1} speed2={speed1}  tripdt2={tripdt1}
XOUT11  QN1i QN1  VDD VGND  CD40_OUT_1X  VDD2={VDD1} speed2={speed1}  tripdt2={tripdt1}
*
XINA2    A2   A2i    VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1}
XINB2    B2   B2i    VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1}
XINCLR2  CLR2 CLR2i  VDD VGND  CD40_IN_1  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1}
XINRC12  RC2  RC2i   VDD VGND  CD40_IN_0  vdd2={vdd1}  speed2={speed1}  tripdt2={tripdt1}
*
A42 DHIGH2 0 N0142 0 N0042 N0062 N0052 0 DFLOP   tripdt={tripdt1}  td={td5}
*A3 0 N015 N016 N023 0  N014 0 0 OR  tripdt={tripdt1}  td={td5}
A32 0 N0152 N0162 0 0     0 N0142 0  OR  tripdt={tripdt1}  td={td5}
A12 A2i 0 0 0 0          0 N0152 0  SCHMITT  Vt=0.46 Vh=0.02  tripdt={tripdt1}  td={td5}
A22 B2i 0 0 0 0          N0162 0 0  SCHMITT  Vt=0.46 Vh=0.02  tripdt={tripdt1}  td={td5}
A62 0 N0082 0 N0062 0     N0072 0 0  AND  tripdt={tripdt1}  td={td5}
A52 0 N0092 0 N0072 0     N0082 0 0  AND  tripdt={tripdt1}  td={td5}
A142 0 N0032 0 N0212 0    N0042 0 0  AND  tripdt={tripdt1}  td={td2}
A132 0 N0032 0 N0052 0    0 N0302 0  AND  tripdt={tripdt1}  td={td5}
A122 N0052 0 0 0 0       N0122 0 0  BUF  tripdt={tripdt1}  td={td5}
A212 0 N0102 0 N0112 0    0 N0092 0  OR  tripdt={tripdt1}  td={td5}
A222 0 N0052 0 N0072 0    N0102 0 0  OR  tripdt={tripdt1}  td={td5}
A112 0 N0132 0 N0122 0    0 N0032 0  OR  tripdt={tripdt1}  td={td5}
A72 0 N0072 0 N0182 0     QN2i Q2i 0  AND  tripdt={tripdt1}  td={td5}
A202 RC2i 0 0 0 0        N0112 0 0  SCHMITT  tripdt={tripdt1}  td={td5}  Vt=0.65 Vh=0.2m
A102 RC2i 0 0 0 0        0 N0132 0  SCHMITT  tripdt={tripdt1}  td={td5}  Vt=0.1 Vh=0.2m
A322 0 N0062 0 N0212 0    N0222 0 0  OR  tripdt={tripdt1}  td={td5}
A312 0 N0222 0 N0232 0    N0212 0 0  OR  tripdt={tripdt1}  td={td2}
A332 N0212 0 0 0 0       N0192 0 0  BUF  tripdt={tripdt1}  td={td5}
A302 CLR2i 0 0 0 0       N0232 0 0  BUF  tripdt={tripdt1}  td={td5}
A342 0 N0072 0 N0062 0    N0202 0 0  AND  tripdt={tripdt1}  td={td5}
A352 0 N0202 0 N0182 0    N0172 0 0  OR  tripdt={tripdt1}  td={td5}
A362 0 N0172 0 N0192 0    N0182 0 0  OR  tripdt={tripdt1}  td={td5}
M102 RC2 N0012 VGND VGND NMOS1
M302 RC2 N0242 VDD VDD PMOS1
D12 RC2 VDD DIO1
D22 VGND RC2 DIO1
R102 N0012 N0022 100
R302 N0242 N0292 100
R32 CGND2 VGND 1
E42 RC2i 0 RC2 VGND {egain}
E72 N0292 VDD N0182 0 {VDD1}
E82 N0022 VGND N0302 0 {VDD1}
V12 DHIGH2 0 DC 1
*
XOUT02  Q2i  Q2   VDD VGND  CD40_OUT_1X  VDD2={VDD1} speed2={speed1}  tripdt2={tripdt1}
XOUT12  QN2i QN2  VDD VGND  CD40_OUT_1X  VDD2={VDD1} speed2={speed1}  tripdt2={tripdt1}
*
*
.MODEL NMOS1 NMOS ( LEVEL=3  W=500e-6  L=4E-6  VTO=1
+ VMAX=1E6  RS=10  RD=10  CJ=0.002  MJ=0.4
+THETA=1E-6 ETA=0.01  KAPPA=2.2)
.MODEL PMOS1 PMOS ( LEVEL=3  W=500e-6  L=4E-6  VTO=-1
+ VMAX=1E6  RS=10  RD=10  CJ=0.002  MJ=0.4
+THETA=1E-6 ETA=0.01  KAPPA=2.2)
.MODEL DIO1 D (IS=10p RS=10 N=1.75 M=0.4 Cjo=6p TT=20n)
*
*
.ends
*
*================================================================
*
*   INPUT FILTERS
*
.MODEL CD40DIO1 D(Is=1e-12 Rs=100)
*
.SUBCKT  CD40_IN_0  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param vt1=0.5
.param gain=(1/{vdd3})
*
*D1 0   in  CD40DIO1
*D2 in VDD  CD40DIO1
R2 in VGND 1e8
E1 out20 0 in VGND {gain}
AE1  out20 0 0 0 0  0 out 0  BUF  ref={vt1}  vhigh=1  tripdt={tripdt3}
.ends
*
*
.SUBCKT  CD40_IN_1  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
* 3ns input delay
*.param Cval = 0.55e-12*4/({vdd3}-0.5)*{speed3}
* 10ns delay @5V
.param Cval = 1.8e-12*5/{vdd3}*{speed3}
.param vt1=0.5
.param gain=(1/{vdd3})
*
*D1 0   in  CD40DIO1
*D2 in VDD  CD40DIO1
R1 in out10 10k
C1 out10 VGND {Cval}
R2 in VGND 1e8
*E1 out20 0 out10 VGND {gain}
B1 out20 0 V=LIMIT(0,V(out10,VGND)*{gain},1)
AE1  out20 0 0 0 0  0 out 0  BUF  ref={vt1}  vhigh=1  tripdt={tripdt3}
.ends
*
*
* Schmitt-input; 2.9V/2.1V @5V
.SUBCKT  CD40_IN_S_1  in out  VDD VGND  vdd3={vdd2}  speed3={speed2}  tripdt3={tripdt2}
.param Cval = 1.8e-12*5/{vdd3}*{speed3}
.param vt1=2.5/5
.param vh1=0.4/5
.param gain=(1/{vdd3})
*
*D1 0   in  CD40DIO1
*D2 in VDD  CD40DIO1
R1 in out10 10k
C1 out10 VGND {Cval}
R2 in VGND 1e8
*E1 out20 0 out10 VGND {gain}
B1 out20 0 V=LIMIT(0,V(out10,VGND)*{gain},1)
AE1  out20 0 0 0 0  0 out 0  SCHMITT  vt={vt1} vh={vh1} vhigh=1  tripdt={tripdt3}
.ends
*
*
*======================================================================
*
* OUTPUT DRIVERS, LEVEL TRANSLATORS
*
*
* Tristate switch
.MODEL SW_HC1 SW(Vt=0.5 Ron=1 Roff=1e6)
.MODEL SW_HC2 SW(Vt=0.5 Ron=1 Roff=1e6)
*
.MODEL DIO2 D(Is=1e-12 Rs=10)
*
* Standard output driver
.SUBCKT  CD40_OUT_1X  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param trise1=80e-9*5.0/{vdd3}*{speed3}
.param Rout=500*5.0/{vdd3}*{speed3}
*
AE1  in 0 0 0 0  0 out10 0  BUF  tripdt={tripdt3}  trise={trise1}
*
E1 out20 VGND out10 0 {vdd3}
Rout out20 out {Rout}
*D1 0   out  DIO2
*D2 out VDD  DIO2
.ends
*
*
*
* Strong output driver
.SUBCKT  CD40_OUT_2X  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param trise1=80e-9*5/{vdd3}*{speed3}
.param Rout=250*5.0/{vdd3}*{speed3}
*
AE1  in 0 0 0 0  0 out10 0  BUF  tripdt={tripdt3}  trise={trise1}
*
E1  out20 VGND out10 0 {vdd3}
Rout  out20 out {Rout}
*D1 0   out  DIO2
*D2 out VDD  DIO2
.ends
*
*
*
* Tristate output driver
.SUBCKT  CD40_OUT_TS_2X  en in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param trise1=80e-9*5.0/{vdd3}*{speed3}
.param Rout=250*5/{vdd3}*{speed3}
*
A1  in 0 0 0 0  0 out10 0  BUF  tripdt={tripdt3}  trise={trise1}
*
E1  out20 VGND out10 0 {vdd3}
Rout  out20 out30 {Rout}
SW1  out30 out en 0 SW_HC1
*D1 0   out  DIO2
*D2 out VDD  DIO2
.ends
*
*
*
* Open drain output driver
.SUBCKT  CD40_OUT_OD_1X  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param trise1=80e-9*5/{vdd3}*{speed3}
.param Rout=500*5/{vdd3}*{speed3}
*
A1  in 0 0 0 0   out10 0 0  BUF  tripdt={tripdt3}  trise={trise1}
*
Rout  out30 VGND {Rout}
SW1  out30 out out10 0 SW_HC2
 *Alternative real output stage from CD40U04 would replace Rout and SW1
 *E1  out20 VGND out10 0 {vdd3}
 *Rout  out20 out30 {Rout}
 *MN1 out out30 VGND VGND  MHCNEN W=140U L=2.4U AD=200P AS=300P PD=10U PS=130U
 *MN2 out out30 VGND VGND  MHCNEN W=140U L=2.4U AD=200P AS=300P PD=10U PS=130U
 *MN3 out out30 VGND VGND  MHCNEN W=140U L=2.4U AD=200P AS=300P PD=10U PS=130U
*D1 0   out  DIO2
*D2 out VDD  DIO2
.ends
*
*
*
